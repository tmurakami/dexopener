apply plugin: 'jacoco'

def jacocoReportTask = tasks.create('jacocoTestReport')
androidVariants.all {
    def variantName = name
    def testTaskName = "test${variantName.capitalize()}UnitTest"
    def jacocoTaskName = "jacoco${testTaskName.capitalize()}Report"
    jacocoReportTask.dependsOn tasks.create(
            name: jacocoTaskName,
            type: JacocoReport,
            dependsOn: testTaskName) {
        reports {
            def reportDir = "$buildDir/reports/jacoco/$testTaskName/$variantName"
            html.enabled = true
            html.destination "$reportDir/html"
            xml.enabled = true
            xml.destination "$reportDir/${jacocoTaskName}.xml"
        }
        sourceDirectories = files(
                android.sourceSets.main.java.srcDirs,
                android.sourceSets[variantName].java.srcDirs,
                android.sourceSets[buildType.name].java.srcDirs,
                productFlavors.collect { android.sourceSets[it.name].java.srcDirs })
        classDirectories = fileTree(
                dir: javaCompiler.destinationDir,
                excludes: ['**/BuildConfig.class', '**/R.class', '**/R$*.class'])
        executionData = files("$buildDir/jacoco/${testTaskName}.exec")
    }
}
